plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs'
}

android {
    compileSdkVersion sdk_compile
    buildFeatures.viewBinding true

    signingConfigs {
        debug {
            storeFile file('keystore/dummy.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile file('keystore/dummy.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    defaultConfig {
        applicationId "com.xsolla.android.storesdkexample"
        minSdkVersion sdk_min
        targetSdkVersion sdk_target
        versionCode sample_app_version_code
        versionName sample_app_version_name
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary true

        buildConfigField 'int', 'PROJECT_ID', '77640' // Replace to your Project ID
        buildConfigField 'String', 'LOGIN_ID', '"026201e3-7e40-11ea-a85b-42010aa80004"' // Replace to your project's Login ID
        buildConfigField 'int', 'OAUTH_CLIENT_ID', '57' // Replace to your project's OAuth2 Client ID
        buildConfigField 'String', 'GOOGLE_CREDENTIAL', "\"${System.env.GOOGLE_SERVER_ID}\""
        buildConfigField 'String', 'FACEBOOK_APP_ID', "\"${System.env.FACEBOOK_APP_ID}\""
        buildConfigField 'String', 'FACEBOOK_CLIENT_TOKEN', "\"${System.env.FACEBOOK_CLIENT_TOKEN}\""
        buildConfigField 'String', 'WEBSHOP_URL', '"https://sitebuilder.xsolla.com/game/sdk-web-store"'
        buildConfigField 'String', 'LOGIN_API_HOST', '"login.xsolla.com"'
        buildConfigField 'boolean', 'IS_SANDBOX', 'true'

        buildConfigField "String", "VERSION_NAME", "\"$versionName\""
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        quiet true
        abortOnError true
        checkDependencies true
    }

}

dependencies {
    implementation project(":app-core")

    implementation project(":xsolla-login-sdk")
    implementation project(":xsolla-login-sdk-google")
    implementation project(":xsolla-store-sdk")
    implementation project(":xsolla-inventory-sdk")
    implementation project(":xsolla-payments-sdk")
    implementation project(":xsolla-googleplay-sdk")
    implementation project(":xsolla-login-sdk-facebook")

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'com.android.billingclient:billing:3.0.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
}
repositories {
    mavenCentral()
}
