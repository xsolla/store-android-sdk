plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'org.jetbrains.dokka'
    id "org.cyclonedx.bom" version "1.4.0"
}

android {
    compileSdkVersion sdk_compile

    resourcePrefix 'xsolla_login_'

    defaultConfig {
        minSdkVersion sdk_min
        targetSdkVersion sdk_target

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        buildConfigField "String", "VERSION_NAME", "\"$login_sdk_version_name\""
    }

    buildTypes {
        release {
            minifyEnabled false
            consumerProguardFiles 'consumer-rules.pro'
        }
    }

    lintOptions {
        quiet true
        abortOnError true
        checkDependencies true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    implementation "androidx.core:core-ktx:$androidx_core_ktx"
    implementation "androidx.browser:browser:$androidx_browser"

    testImplementation "junit:junit:$junit_version"
    testImplementation "org.robolectric:robolectric:$robolectric_version"
    testImplementation "androidx.test:core:$androidx_test_core"
    testImplementation 'com.facebook.android:facebook-login:14.0.0'
    testImplementation 'com.google.android.gms:play-services-auth:20.2.0'
    testImplementation 'com.tencent.mm.opensdk:wechat-sdk-android-without-mta:6.6.19'
    testImplementation fileTree(dir: 'libs', include: ['*.jar'])

    compileOnly 'com.facebook.android:facebook-login:14.0.0'
    compileOnly 'com.google.android.gms:play-services-auth:20.2.0'
    compileOnly 'com.tencent.mm.opensdk:wechat-sdk-android-without-mta:6.6.19'
    compileOnly fileTree(dir: 'libs', include: ['*.jar'])
}

dokkaHtmlPartial {
    moduleName.set("Xsolla Login SDK for Android")
    dokkaSourceSets {
        main {
            includes.from("Module.md")
            documentedVisibilities.set(
                    [org.jetbrains.dokka.DokkaConfiguration.Visibility.PUBLIC]
            )
            skipDeprecated.set(true)
            sourceLink {
                localDirectory.set(file("src/main/java"))
                remoteUrl.set(new URL(
                        "https://github.com/xsolla/store-android-sdk/tree/master/xsolla-login-sdk/src/main/java"))
                remoteLineSuffix.set("#L")
            }
            displayName.set("Login SDK")
        }
    }
}

ext {
    PUBLISH_GROUP_ID = 'com.xsolla.android'
    PUBLISH_ARTIFACT_ID = 'login'
    PUBLISH_VERSION = login_sdk_version_name
    PUBLISH_NAME = 'Xsolla Login SDK for Android'
    PUBLISH_DESCRIPTION = 'Xsolla Login SDK for Android is used to integrate Xsolla Login, a single sign-on tool that uses API methods to authenticate and secure user passwords. This creates a seamless one-click registration experience players can use for fast and safe transactions across all of your games.'
}

apply from: "${rootDir}/publishMavenCentral.gradle"

apply from: 'tests.gradle'

cyclonedxBom {
    includeConfigs += ["releaseCompileClasspath"]
}