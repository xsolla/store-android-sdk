plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'org.jetbrains.dokka'
    id "org.cyclonedx.bom" version "1.4.0"
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlin_version" // for :lib-login-api code
}

android {
    compileSdkVersion sdk_compile

    resourcePrefix 'xsolla_login_'

    defaultConfig {
        minSdkVersion sdk_min
        targetSdkVersion sdk_target

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        buildConfigField "String", "VERSION_NAME", "\"$login_sdk_version_name\""
    }

    buildTypes {
        release {
            minifyEnabled false
            consumerProguardFiles 'consumer-rules.pro'
        }
    }

    lintOptions {
        quiet true
        abortOnError true
        checkDependencies true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    // for :lib-login-api code
    sourceSets {
        main.java.srcDirs += '../lib-login-api/src/main/java'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    implementation "androidx.core:core-ktx:$androidx_core_ktx"
    implementation "androidx.browser:browser:$androidx_browser"
    implementation "com.google.code.gson:gson:2.9.1" // for JWT parser only

    testImplementation "junit:junit:$junit_version"
    testImplementation "org.robolectric:robolectric:$robolectric_version"
    testImplementation "androidx.test:core:$androidx_test_core"
    testImplementation 'com.facebook.android:facebook-login:14.0.0'
    testImplementation 'com.google.android.gms:play-services-auth:20.3.0'
    testImplementation 'com.tencent.mm.opensdk:wechat-sdk-android-without-mta:6.8.0'
    testImplementation fileTree(dir: 'libs', include: ['*.jar'])

    compileOnly 'com.facebook.android:facebook-login:14.0.0'
    compileOnly 'com.google.android.gms:play-services-auth:20.3.0'
    compileOnly 'com.tencent.mm.opensdk:wechat-sdk-android-without-mta:6.8.0'
    compileOnly fileTree(dir: 'libs', include: ['*.jar'])

//    implementation project(":lib-login-api")
    // for :lib-login-api code
    implementation("io.ktor:ktor-client-core:$ktor_version")
    implementation("io.ktor:ktor-client-okhttp:$ktor_version")
//    implementation("io.ktor:ktor-client-logging:$ktor_version")
    implementation("io.ktor:ktor-client-content-negotiation:$ktor_version")
    implementation("io.ktor:ktor-serialization-kotlinx-json:$ktor_version")

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"
}

dokkaHtmlPartial {
    moduleName.set("Xsolla Login SDK for Android")
    dokkaSourceSets {
        main {
            includes.from("Module.md")
            documentedVisibilities.set(
                    [org.jetbrains.dokka.DokkaConfiguration.Visibility.PUBLIC]
            )
            skipDeprecated.set(true)
            sourceLink {
                localDirectory.set(file("src/main/java"))
                remoteUrl.set(new URL(
                        "https://github.com/xsolla/store-android-sdk/tree/master/xsolla-login-sdk/src/main/java"))
                remoteLineSuffix.set("#L")
            }
            displayName.set("Login SDK")
        }
    }
}

ext {
    PUBLISH_GROUP_ID = 'com.xsolla.android'
    PUBLISH_ARTIFACT_ID = 'login'
    PUBLISH_VERSION = login_sdk_version_name
    PUBLISH_NAME = 'Xsolla Login SDK for Android'
    PUBLISH_DESCRIPTION = 'Xsolla Login SDK for Android is used to integrate Xsolla Login, a single sign-on tool that uses API methods to authenticate and secure user passwords. This creates a seamless one-click registration experience players can use for fast and safe transactions across all of your games.'
}

apply from: "${rootDir}/publishMavenCentral.gradle"

apply from: 'tests.gradle'

cyclonedxBom {
    includeConfigs += ["releaseCompileClasspath"]
}