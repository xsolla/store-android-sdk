plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'org.jetbrains.dokka'
    id "org.cyclonedx.bom" version "1.4.0"
}

android {
    compileSdkVersion sdk_compile

    resourcePrefix 'xsolla_store_'

    defaultConfig {
        minSdkVersion sdk_min
        targetSdkVersion sdk_target

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        buildConfigField "String", "VERSION_NAME", "\"$store_sdk_version_name\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        quiet true
        abortOnError true
        checkDependencies true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    testImplementation "junit:junit:$junit_version"
    testImplementation "org.robolectric:robolectric:$robolectric_version"
}

dokkaHtml.configure {
    dokkaSourceSets {
        named("main") {
            moduleName.set("Xsolla Store SDK for Android")
            includes.from("Module.md")
        }
    }
}

ext {
    PUBLISH_GROUP_ID = 'com.xsolla.android'
    PUBLISH_ARTIFACT_ID = 'store'
    PUBLISH_VERSION = store_sdk_version_name
    PUBLISH_NAME = 'Xsolla Store SDK for Android'
    PUBLISH_DESCRIPTION = 'Xsolla Store SDK for Android is used to integrate Xsolla Store, a comprehensive e-store solution for partners that supports various monetization options'
}

apply from: "${rootDir}/publishMavenCentral.gradle"

apply from: 'tests.gradle'

cyclonedxBom {
    includeConfigs += ["releaseCompileClasspath"]
}