plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'org.jetbrains.dokka'
    id "org.cyclonedx.bom" version "1.4.0"
}

android {
    compileSdkVersion sdk_compile

    resourcePrefix 'xsolla_inventory_'

    defaultConfig {
        minSdkVersion sdk_min
        targetSdkVersion sdk_target

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        buildConfigField "String", "VERSION_NAME", "\"$inventory_sdk_version_name\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        quiet true
        abortOnError true
        checkDependencies true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    testImplementation "junit:junit:$junit_version"
    testImplementation "org.robolectric:robolectric:$robolectric_version"
}

dokkaHtmlPartial {
    moduleName.set("Xsolla Inventory SDK for Android")
    dokkaSourceSets {
        main {
            includes.from("Module.md")
            documentedVisibilities.set(
                    [org.jetbrains.dokka.DokkaConfiguration.Visibility.PUBLIC]
            )
            sourceLink {
                localDirectory.set(file("src/main/java"))
                remoteUrl.set(new URL(
                        "https://github.com/xsolla/store-android-sdk/tree/master/xsolla-inventory-sdk/src/main/java"))
                remoteLineSuffix.set("#L")
            }
            displayName.set("Inventory SDK")
        }
    }
}

ext {
    PUBLISH_GROUP_ID = 'com.xsolla.android'
    PUBLISH_ARTIFACT_ID = 'inventory'
    PUBLISH_VERSION = inventory_sdk_version_name
    PUBLISH_NAME = 'Xsolla Inventory SDK for Android'
    PUBLISH_DESCRIPTION = 'Xsolla Inventory SDK for Android is used to integrate applications based on Android with Player Inventory for managing: user inventory, virtual currency balance, cross-platform inventory'
}

apply from: "${rootDir}/publishMavenCentral.gradle"

apply from: 'tests.gradle'

cyclonedxBom {
    includeConfigs += ["releaseCompileClasspath"]
}